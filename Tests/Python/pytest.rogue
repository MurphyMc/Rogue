global rogue_global = 121

routine rogue_routine ()->Int32
  local r = rogue_global
  rogue_global++
  return r
endRoutine


class MyCompound(cp_int=43:Int32, cp_real=1.0:Real32, cp_str="hi":String) [compound]
  METHODS
    method f_c_sum ()->Real64
      return cp_int->Real64 + cp_real

  GLOBAL METHODS
    method gf_int (x : Int32)->Int32
      return x + 1

    method gf_compound (c : MyCompound, x : Int32)->Int32
      return x + c.cp_int
endClass

class Foo [api]
  GLOBAL PROPERTIES
    gprop_comp = MyCompound(11,22,"okay")
    gprop_int = 2000

  PROPERTIES
    prop_str = "hello"
    prop_int = 42
    prop_bar : Bar
    prop_f_v_i : Function()->Int32
    prop_f_s_i : Function(String)->Int32
    prop_f_s_b : Function(String)->Bar
    prop_f_s_o : Function(String)->Object
    prop_f_i_s : Function(Int32)->String
    prop_f_exception : Function()
    prop_comp = MyCompound(1,2,"ok")

  GLOBAL METHODS
    method gf_square (i : Int64) -> Int64
      return i*i

    method gf_compound (c : MyCompound)->MyCompound
      gprop_int = c.cp_int
      c.cp_int += 1
      return c

    method gf_overload ()->String
      return "Overload1"
    method gf_overload (x:Int32)->String
      return "Overload2"

  METHODS
    method f_ss (msg : String) -> String [api]
      return msg.to_uppercase()

    method f_sf (msg : String) -> (Function()->String)
      local bar = function ()->String with (msg)
        #println "Rogue callback returning '" + msg + "' in uppercase"
        return msg.to_uppercase()
      endFunction
      return bar

    method new_bar (val : String) -> Bar
      return Bar(val)

    method call_funcs () -> Logical
      if (not prop_f_v_i) return false
      if (not prop_f_s_i) return false
      if (not prop_f_s_o) return false
      if (not prop_f_s_b) return false

      if (prop_f_v_i() != 99) return false

      if (prop_f_s_i("33") != 330) return false

      if (prop_f_s_o("FuncBar")->String != "<Bar PyFuncBar>") return false

      if (prop_f_s_b("FuncBar").val != "PyFuncBar") return false

      if (prop_f_i_s(4) != "!!!!") return false

      return true

    method f_opt_s (msg = "hi" : String)->String
      return msg

    method f_opt_i (n = 2 : Int32)->Int32
      return n * n

    method f_over () -> String
      return "Nothing"

    method f_over (i : Int32) -> String
      return "Int32"

    method f_over (s : String) -> String
      return "String"

    method f_over (i : Int32, s : String) -> String
      return "Int32,String"

    method f_over (i : Int32, o : Object) -> String
      return "Int32,Object"

    method f_rogue_exception ()
      throw Error("This is from Rogue")

    method f_python_exception () -> String
      try
        prop_f_exception()
      catch (err:Exception)
        return err->String
      endTry
      return ""

    method f_compound_return () -> MyCompound
      #println "Returning compound from:" + this + " prop_str:" + this.prop_str
      local x = MyCompound()
      x.cp_str = prop_str
      return x

    method f_take_compound (c : MyCompound) -> Int32
      #println "Take compound:" + c.cp_int + " from:" + this + " prop_str:" + this.prop_str
      return c.cp_int
endClass


class Bar
  PROPERTIES
    val = "DefaultBar"

  METHODS
    method init (v: String)
      val = v

    method to->String
      return "<Bar $>" (val)
endClass
