class Property
  PROPERTIES
    t             : Token
    type_context  : Type
    name          : String
    type          : Type
    attributes    : Int32
    initial_value : Cmd

  METHODS
    method init( t, type_context, name, type=null, initial_value=null )

    method cloned->Property
      local result = Property( t, type_context, name )

      <init_result>
      result.type = type
      result.attributes = attributes
      if (initial_value) result.initial_value = initial_value.cloned
      return result

    method is_global->Logical
      return (attributes & Attribute.is_global)?

    method is_incorporated->Logical
      return (attributes & Attribute.is_incorporated)?

    method is_native->Logical
      return (attributes & Attribute.is_native)?

    method is_thread_local->Logical
      return (attributes & Attribute.is_thread_local)?

    method mark_incorporated->this
      attributes |= Attribute.is_incorporated
      return this

    method mark_native->this
      attributes |= Attribute.is_native
      return this

    method set_type_context( @type_context )->Property
      return this

    method to->String
      return name
endClass

